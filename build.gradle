group 'org.paggarwal'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.liquibase:liquibase-gradle-plugin:1.2.1"
        classpath 'nu.studer:gradle-jooq-plugin:1.0.6'
        classpath 'mysql:mysql-connector-java:5.1.38'
    }
    configurations.classpath {
        resolutionStrategy {                            // enforce a specific jOOQ version
            forcedModules = [
                    'org.jooq:jooq:3.7.2',
                    'org.jooq:jooq-meta:3.7.2',
                    'org.jooq:jooq-codegen:3.7.2'
            ]
        }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.liquibase.gradle'
apply plugin: 'nu.studer.jooq'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'javax.inject:javax.inject:1'
    compile 'com.google.guava:guava:19.0'

    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.1'

    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'org.jooq:jooq:3.7.2'
    compile 'org.apache.tomcat:tomcat-jdbc:8.0.32'
    compile 'org.liquibase:liquibase-core:3.4.2'

    compile 'org.springframework:spring-core:4.2.5.RELEASE'
    compile 'org.springframework:spring-context:4.2.5.RELEASE'
    compile 'org.springframework:spring-tx:4.2.5.RELEASE'
    compile 'org.springframework:spring-jdbc:4.2.5.RELEASE'

    // mandatory dependencies for using Spock
    compile "org.codehaus.groovy:groovy-all:2.4.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor (together with CGLIB)
}

sourceSets.main.java.srcDir "${projectDir}/src/main/generated"

liquibase {
    activities {
        main {
            changeLogFile 'src/main/db/changelogs.xml'
            url 'jdbc:mysql://localhost:3306/rancherscheduler'
            username 'dev'
            password 'dev'
        }
    }
}

jooq {
    db(sourceSets.main) {
        jdbc {
            driver = 'com.mysql.jdbc.Driver'
            url = 'jdbc:mysql://localhost:3306/rancherscheduler'
            user = 'root'
            password = 'root'
            schema = 'rancherscheduler'
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mysql.MySQLDatabase'
                inputSchema = 'rancherscheduler'
                dateAsTimestamp = true
                unsignedTypes = false
                includes = ".*"
                excludes = "DATABASECHANGELOG|DATABASECHANGELOGLOCK"
            }
            target {
                packageName = 'org.paggarwal.rancherscheduler.generated'
                directory = "${projectDir}/src/main/generated"
            }
        }
    }
}
